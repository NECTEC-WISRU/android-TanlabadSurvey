/*
 * Copyright (c) 2016 NECTEC
 *   National Electronics and Computer Technology Center, Thailand
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

group = 'org.tanrabad'
version = {->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    String tag = stdout.toString().trim();
    if (tag.length() == 0)
        tag = "0.0.0"
    println "version $tag"
    return tag
}.call()

import java.text.SimpleDateFormat

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'com.github.triplet.gradle:play-publisher:1.2.0'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

ext {
    isCi = ("true".equals(System.getenv("TRAVIS")) || "true".equals(System.getenv("CIRCLECI")))

    checkstyleVersion = "6.16.1"
    checkstyleConfigFile = file("$rootDir/config/checkstyle.xml")
    pmdRuleSetFiles = files("$rootDir/config/pmd.xml")

    javaVersion = JavaVersion.VERSION_1_7

    def versionToken = project.version.tokenize( ".|-" )
    int versionMajor = versionToken[0] as Integer
    int versionMinor = versionToken[1] as Integer
    int versionPatch = versionToken[2] as Integer
    versionName = project.version
    versionCode = versionMajor * 10000 + versionMinor * 1000 + versionPatch
    buildTime = new SimpleDateFormat("yyMMddHHmm").format(new Date())

    clientId = System.getenv("TRB_AUTHEN_CLIENT_ID")
    clientSecret = System.getenv("TRB_AUTHEN_CLIENT_SECRET")

    apiBaseUrlTest = "http://trb-test.igridproject.info/v1"
    apiBaseUrlProduction = "https://api.tanrabad.org/v1"

    jodaTimeVersion = "2.9.1"
    junitVersion = "4.12"
}

task wrapper(type: Wrapper) {
    description 'Creates the gradle wrapper.'
    gradleVersion '3.5.1'
}
