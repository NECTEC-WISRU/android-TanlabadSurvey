/*
 * Copyright (c) 2016 NECTEC
 *   National Electronics and Computer Technology Center, Thailand
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

version versionName

buildscript {
    repositories {
        maven { url "https://maven.fabric.io/public" }
        maven { url "https://jitpack.io" }
        jcenter()
    }

    dependencies {
        classpath "io.fabric.tools:gradle:1.21.6"
        classpath "com.neenbedankt.gradle.plugins:android-apt:1.8"
        classpath 'com.google.gms:google-services:2.1.0'
        classpath 'com.noveogroup.android:check:1.2.3'
    }
}

apply plugin: "com.android.application"
apply plugin: "io.fabric"
apply plugin: "com.neenbedankt.android-apt"
apply plugin: "com.noveogroup.android.check"

repositories {
    maven { url "https://maven.fabric.io/public" }

}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "org.tanrabad.survey"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        testApplicationId "org.tanrabad.survey.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        manifestPlaceholders = [manifestApplicationId: "${applicationId}",
                                onesignal_app_id: "b92149c9-8d41-4bf7-aedb-3c6c3cb65c4c",
                                onesignal_google_project_number: "139178348155"]
        resValue "string", "app_build_time", "build $buildTime"
        buildConfigField "String", "TRB_AUTHEN_CLIENT_ID", "\"$clientId\""
        buildConfigField "String", "TRB_AUTHEN_CLIENT_SECRET", "\"$clientSecret\""
        buildConfigField "String", "API_BASE_URL_TEST", "\"$apiBaseUrlTest\""
        buildConfigField "int", "SURVEY_RANGE_DAY", "1"
    }

    compileOptions {
        sourceCompatibility rootProject.javaSourceCompatibility
        targetCompatibility rootProject.javaTargetCompatibility
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            storeFile file("release.keystore")
            keyAlias System.getenv("TANRABAD_KEY_ALIAS")
            keyPassword System.getenv("TANRABAD_KEY_PASSWORD")
            storePassword System.getenv("TANRABAD_STORE_PASSWORD")
        }
    }

    buildTypes {
        debug {
            debuggable true
            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false

            manifestPlaceholders = [google_map_key: System.getenv("TANRABAD_GOOGLE_DEBUG_KEY")]
            buildConfigField "String", "API_BASE_URL", "\"$apiBaseUrlTest\""
            buildConfigField "String", "TRIAL_USER", "\"trial-debug\""
            resValue "string", "app_version", "version $versionName-Debug"
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"

            manifestPlaceholders = [google_map_key: System.getenv("TANRABAD_GOOGLE_RELEASE_KEY")]
            buildConfigField "String", "API_BASE_URL", "\"$apiBaseUrlProduction\""
            buildConfigField "String", "TRIAL_USER", "\"trial-release\""
            resValue "string", "app_version", "version $versionName"
        }
    }

    lintOptions {
        abortOnError true
        checkAllWarnings true
        warningsAsErrors true
        disable "IconColors", "InvalidPackage", "TrustAllX509TrustManager", "NewerVersionAvailable", "IconExpectedSize",
                "GradleDependency", "StaticFieldLeak", "HardwareIds"
    }

    packagingOptions {
        exclude "LICENSE.txt"
        exclude "META-INF/DEPENDENCIES"
        exclude "META-INF/ASL2.0"
        exclude "META-INF/NOTICE"
        exclude "META-INF/LICENSE"
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    dexOptions {
        if (isCi) {
            incremental false
            preDexLibraries false
        }
    }
    useLibrary "org.apache.http.legacy"

}

dependencies {
    apt "com.bluelinelabs:logansquare-compiler:$loganSquareVersion"

    compile fileTree(dir: "libs", include: ["*.jar"])
    compile project(":domain")
    compile "com.onesignal:OneSignal:$oneSignalVersion@aar"
    compile "com.android.support:cardview-v7:$supportLibVersion"
    compile "com.android.support:preference-v14:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:percent:$supportLibVersion"
    compile "com.google.android.gms:play-services-gcm:$playServicesVersion"
    compile "com.google.android.gms:play-services-appindexing:$playServicesVersion"
    compile "com.google.android.gms:play-services-location:$playServicesVersion"
    compile "com.google.android.gms:play-services-maps:$playServicesVersion"
    compile "com.google.android.gms:play-services-analytics:$playServicesVersion"
    compile("com.google.maps.android:android-maps-utils:$androidMapUtilsVersion") {
        exclude(group: "com.google.android.gms")
    }
    compile "com.bartoszlipinski.recyclerviewheader:library:$recyclerViewHeaderVersion"
    compile "com.bluelinelabs:logansquare:$loganSquareVersion"
    compile("com.crashlytics.sdk.android:answers:$answersVersion@aar") { transitive = true; }
    compile("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion@aar") { transitive = true; }
    compile "com.squareup.okhttp3:okhttp:$okHttpVersion"
    compile "joda-time:joda-time:$jodaTimeVersion"
    compile "net.frakbot:jumpingbeans:$jumpingbeansVersion"
    compile "th.or.nectec.android:thai-widget:$thaiWidgetVersion"
    compile "uk.co.chrisjenx:calligraphy:$calligraphyVersion"
    compile "com.artit-k:license-fragment:$licenseFragmentVersion"
    compile "cat.ereza:customactivityoncrash:$customActivityOnCrash"
    compile "de.hdodenhof:circleimageview:$circleImageViewVersion"
    compile "it.sephiroth.android.library.targettooltip:target-tooltip-library:$tooltipVersion"

    testCompile "junit:junit:$junitVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"
    testCompile "org.slf4j:slf4j-simple:$slf4jVersion"
    testCompile "com.github.tomakehurst:wiremock:$wiremockVersion"

    androidTestCompile "com.android.support:support-annotations:$supportLibVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$espressoVersion"
    androidTestCompile "com.android.support.test:runner:$espressoRuleRunnerVersion"
    androidTestCompile "com.android.support.test:rules:$espressoRuleRunnerVersion"
    androidTestCompile "org.mockito:mockito-core:$mockitoVersion"
    androidTestCompile "com.google.dexmaker:dexmaker:$dexmakerVersion"
    androidTestCompile("com.google.dexmaker:dexmaker-mockito:$dexmakerVersion") {
        exclude module: "hamcrest-core"
        exclude module: "objenesis"
        exclude module: "mockito-core"
    }
}

apply plugin: 'com.google.gms.google-services'

configurations.all { resolutionStrategy.force "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"}

check {
    checkstyle {
        abortOnError true
        config checkstyleConfigFile
    }
    findbugs {
        skip true
    }
    pmd {
        config pmdRuleSetFiles.getAt(0)
    }
}
