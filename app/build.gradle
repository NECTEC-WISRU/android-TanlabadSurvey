/*
 * Copyright (c) 2016 NECTEC
 *   National Electronics and Computer Technology Center, Thailand
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

version versionName

buildscript {
    repositories {
        maven { url "https://maven.fabric.io/public" }
        maven { url "https://jitpack.io" }
        jcenter()
    }

    dependencies {
        classpath "io.fabric.tools:gradle:1.24.4"
        classpath 'com.google.gms:google-services:2.1.0'
        classpath 'com.noveogroup.android:check:1.2.3'
    }
}

apply plugin: "com.android.application"
apply plugin: "com.github.triplet.play"
apply plugin: "io.fabric"
apply plugin: "com.noveogroup.android.check"

repositories {
    maven { url "https://maven.fabric.io/public" }
}

android {
    compileSdkVersion 25

    playAccountConfigs {
        defaultAccountConfig {
            jsonFile = file('wnp-service-account-key.json')
        }
    }

    defaultConfig {
        applicationId "org.tanrabad.survey"
        minSdkVersion 18
        targetSdkVersion 25
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        testApplicationId "org.tanrabad.survey.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [manifestApplicationId: "${applicationId}",
                                onesignal_app_id: "b92149c9-8d41-4bf7-aedb-3c6c3cb65c4c",
                                onesignal_google_project_number: "139178348155"]
        resValue "string", "app_build_time", "build $buildTime"
        buildConfigField "String", "TRB_AUTHEN_CLIENT_ID", "\"$clientId\""
        buildConfigField "String", "TRB_AUTHEN_CLIENT_SECRET", "\"$clientSecret\""
        buildConfigField "String", "API_BASE_URL_TEST", "\"$apiBaseUrlTest\""
        buildConfigField "int", "SURVEY_RANGE_DAY", "1"

        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }

    compileOptions {
        sourceCompatibility rootProject.javaVersion
        targetCompatibility rootProject.javaVersion
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            storeFile file("release.keystore")
            keyAlias System.getenv("TANRABAD_KEY_ALIAS")
            keyPassword System.getenv("TANRABAD_KEY_PASSWORD")
            storePassword System.getenv("TANRABAD_STORE_PASSWORD")
        }
    }

    buildTypes {
        debug {
            debuggable true
            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false

            def googleMapsKey = System.getenv("TANRABAD_GOOGLE_DEBUG_KEY")
            manifestPlaceholders = [google_map_key: googleMapsKey]
            buildConfigField "String", "GOOGLE_MAP_KEY", "\"$googleMapsKey\""
            buildConfigField "String", "API_BASE_URL", "\"$apiBaseUrlTest\""
            buildConfigField "String", "TRIAL_USER", "\"trial-debug\""
            resValue "string", "app_version", "version $versionName-Debug"
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            def googleMapsKey = System.getenv("TANRABAD_GOOGLE_RELEASE_KEY")
            manifestPlaceholders = [google_map_key: googleMapsKey]
            buildConfigField "String", "GOOGLE_MAP_KEY", "\"$googleMapsKey\""
            buildConfigField "String", "API_BASE_URL", "\"$apiBaseUrlProduction\""
            buildConfigField "String", "TRIAL_USER", "\"trial-release\""
            resValue "string", "app_version", "version $versionName"
        }
    }

    lintOptions {
        abortOnError true
        checkAllWarnings true
        disable "MissingRegistered", "IconColors", "InvalidPackage", "TrustAllX509TrustManager", "NewerVersionAvailable", "IconExpectedSize",
                "GradleDependency", "StaticFieldLeak", "HardwareIds", "OldTargetApi"
    }

    packagingOptions {
        exclude "LICENSE.txt"
        exclude "META-INF/DEPENDENCIES"
        exclude "META-INF/ASL2.0"
        exclude "META-INF/NOTICE"
        exclude "META-INF/LICENSE"
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    dexOptions {
        if (isCi) {
            incremental false
            preDexLibraries false
        }
    }
    useLibrary "org.apache.http.legacy"
}

dependencies {
    def loganSquareVersion = "1.3.6"
    def supportLibVersion = "25.3.1"
    def playServicesVersion = "8.4.0"

    annotationProcessor "com.bluelinelabs:logansquare-compiler:$loganSquareVersion"

    implementation fileTree(dir: "lib", include: ["*.jar"])
    implementation project(":domain")
    implementation project(":domain-nearby")
    implementation "com.onesignal:OneSignal:2.3.0@aar"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation "com.android.support:preference-v14:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:percent:$supportLibVersion"
    implementation "com.google.android.gms:play-services-gcm:$playServicesVersion"
    implementation "com.google.android.gms:play-services-location:$playServicesVersion"
    implementation "com.google.android.gms:play-services-maps:$playServicesVersion"
    implementation "com.google.android.gms:play-services-analytics:$playServicesVersion"
    implementation("com.google.maps.android:android-maps-utils:0.4.3") { exclude(group: "com.google.android.gms") }
    implementation "com.bartoszlipinski.recyclerviewheader:library:1.2.1"
    implementation "com.bluelinelabs:logansquare:$loganSquareVersion"
    implementation('com.crashlytics.sdk.android:answers:1.3.11@aar') { transitive = true }
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') { transitive = true }
    implementation "com.squareup.okhttp3:okhttp:3.8.0"
    implementation "joda-time:joda-time:$jodaTimeVersion"
    implementation "net.frakbot:jumpingbeans:1.3.0"
    implementation "th.or.nectec.android:thai-widget:0.6.2"
    implementation "uk.co.chrisjenx:calligraphy:2.3.0"
    implementation "com.artit-k:license-fragment:1.3.0"
    implementation "cat.ereza:customactivityoncrash:1.5.0"
    implementation "de.hdodenhof:circleimageview:2.2.0"
    implementation "me.piruin:spinney:0.2.2"
    implementation "it.sephiroth.android.library.targettooltip:target-tooltip-library:1.3.14"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.slf4j:slf4j-simple:1.7.22"
    testImplementation "org.skyscreamer:jsonassert:1.5.0"
    testImplementation "com.github.tomakehurst:wiremock:1.57"

    def dexmakerVersion = "1.2"
    def espressoRuleRunnerVersion = "0.5"
    def espressoVersion = "2.2.2"

    androidTestImplementation "com.android.support:support-annotations:$supportLibVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation "com.android.support.test:runner:$espressoRuleRunnerVersion"
    androidTestImplementation "com.android.support.test:rules:$espressoRuleRunnerVersion"
    androidTestImplementation "org.mockito:mockito-core:$mockitoVersion"
    androidTestImplementation "com.google.dexmaker:dexmaker:$dexmakerVersion"
    androidTestImplementation("com.google.dexmaker:dexmaker-mockito:$dexmakerVersion") {
        exclude module: "hamcrest-core"
        exclude module: "objenesis"
        exclude module: "mockito-core"
    }
}

apply plugin: 'com.google.gms.google-services'

configurations.all { resolutionStrategy.force "com.fasterxml.jackson.core:jackson-core:2.6.0"}

check {
    checkstyle {
        abortOnError true
        config checkstyleConfigFile
    }
    findbugs {
        skip true
    }
    pmd {
        config pmdRuleSetFiles.getAt(0)
    }
}
