/*
 * Copyright (c) 2015 NECTEC
 *   National Electronics and Computer Technology Center, Thailand
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.20.1'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply from: '../gradle/versionGenerator.gradle'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}


android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "th.or.nectec.tanrabad.survey"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.genVersionCode
        versionName rootProject.ext.genVersionName
        testApplicationId "th.or.nectec.tanrabad.survey.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility rootProject.ext.javaSourceCompatibility
        targetCompatibility rootProject.ext.javaTargetCompatibility
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
        }
        release {
            storeFile file('release.keystore')
            keyAlias System.getenv('TANRABAD_KEY_ALIAS')
            keyPassword System.getenv('TANRABAD_KEY_PASSWORD')
            storePassword System.getenv('TANRABAD_STORE_PASSWORD')
        }
    }

    buildTypes {
        debug {
            ext.enableCrashlytics = false
            manifestPlaceholders = [google_map_key: System.env.'TANRABAD_GOOGLE_DEBUG_KEY']
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [google_map_key: System.getenv('TANRABAD_GOOGLE_RELEASE_KEY')]
        }
    }

    lintOptions {
        abortOnError true
        checkAllWarnings true
        warningsAsErrors true

        disable 'IconColors', 'InvalidPackage'
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    apt 'com.bluelinelabs:logansquare-compiler:' + rootProject.ext.loganSquareVersion

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':domain')
    compile project(':entity')
    compile 'com.android.support:cardview-v7:' + rootProject.ext.supportLibVersion
    compile 'com.android.support:design:' + rootProject.ext.supportLibVersion
    compile 'com.android.support:percent:' + rootProject.ext.supportLibVersion
    compile 'com.google.android.gms:play-services-location:' + rootProject.ext.playServicesVersion
    compile 'com.google.android.gms:play-services-maps:' + rootProject.ext.playServicesVersion
    compile('com.google.maps.android:android-maps-utils:' + rootProject.ext.androidMapUtilsVersion) {
        exclude(group: 'com.google.android.gms')
    }

    compile 'com.bartoszlipinski.recyclerviewheader:library:1.2.0'
    compile('com.bluelinelabs:logansquare:' + rootProject.ext.loganSquareVersion)
    compile('com.crashlytics.sdk.android:answers:1.3.3@aar') { transitive = true; }
    compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') { transitive = true; }
    compile ('com.squareup.okhttp:okhttp:' + rootProject.ext.okHttpVersion)
    compile 'joda-time:joda-time:' + rootProject.ext.jodaTimeVersion
    compile 'th.or.nectec.android:thai-widget:' + rootProject.ext.thaiWidgetVersion
    compile 'uk.co.chrisjenx:calligraphy:' + rootProject.ext.calligraphyVersion

    testCompile 'junit:junit:' + rootProject.ext.junitVersion
    testCompile 'org.mockito:mockito-core:' + rootProject.ext.mockitoVersion
    testCompile 'org.robolectric:robolectric:' + rootProject.ext.robolectricVersion
    testCompile 'org.slf4j:slf4j-simple:' + rootProject.ext.slf4jVersion
    testCompile 'com.github.tomakehurst:wiremock:' + rootProject.ext.wiremockVersion
    testCompile 'com.google.code.gson:gson:' + rootProject.ext.gsonVersion

    androidTestCompile 'com.android.support:support-annotations:' + rootProject.ext.supportLibVersion
    androidTestCompile 'com.android.support.test.espresso:espresso-core:' + rootProject.ext.espressoVersion
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:' + rootProject.ext.espressoVersion
    androidTestCompile 'com.android.support.test:runner:' + rootProject.ext.runnerVersion
    androidTestCompile 'com.android.support.test:rules:' + rootProject.ext.rulesVersion
}

configurations.all { resolutionStrategy.force "com.fasterxml.jackson.core:jackson-core:"+ rootProject.ext.jacksonVersion }
